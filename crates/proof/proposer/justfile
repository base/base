# justfile for proposer

# Default recipe
default:
    @just --list

# Build all workspace crates
build:
    cargo build --workspace

# Build in release mode
build-release:
    cargo build --workspace --release

# Run all tests
test:
    cargo test --workspace

# Run tests with nextest
test-nextest:
    cargo nextest run --workspace

# Run clippy lints
clippy:
    cargo clippy --workspace --all-targets -- -D warnings

# Format code
fmt:
    cargo +nightly fmt --all

# Check formatting
fmt-check:
    cargo +nightly fmt --all -- --check

# Run cargo deny
deny:
    cargo deny check

# Clean build artifacts
clean:
    cargo clean

# Run the proposer binary
run *ARGS:
    cargo run --bin base-proposer -- {{ARGS}}

# Run the proposer with development configuration (Base Sepolia)
# Uses values from chart/configurations/development/values.yaml
# Override ENCLAVE_RPC for local testing: just run-dev --enclave-rpc http://localhost:1234
run-dev *ARGS:
    cargo run --bin base-proposer -- \
        --l1-eth-rpc https://c3-chainproxy-eth-sepolia-full-dev.cbhq.net \
        --l2-eth-rpc https://base-sepolia-dev.cbhq.net:8545 \
        --rollup-rpc https://base-sepolia-archive-k8s-dev.cbhq.net:7545 \
        --enclave-rpc https://base-proofs-nitro-prover-dev.cbhq.net \
        --onchain-verifier-addr 0xaddD538424b2549a13deE55dE55174516Ed037BC \
        --poll-interval 6s \
        --min-proposal-interval 1 \
        --l2-reth \
        --skip-tls-verify \
        --metrics-enabled \
        --metrics-addr 0.0.0.0 \
        --metrics-port 7300 \
        -vvvv \
        {{ARGS}}

# Run the proposer with local enclave server (for testing Generate() locally)
# Start the enclave server first: cd op-enclave/rust && make run-server
run-local *ARGS:
    cargo run --bin base-proposer -- \
        --l1-eth-rpc https://c3-chainproxy-eth-sepolia-full-dev.cbhq.net \
        --l2-eth-rpc https://base-sepolia.cbhq.net \
        --rollup-rpc https://base-sepolia-archive-k8s-dev.cbhq.net:7545 \
        --enclave-rpc http://localhost:1234 \
        --onchain-verifier-addr 0x0000000000000000000000000000000000000000 \
        --poll-interval 5s \
        --min-proposal-interval 1 \
        --l2-reth \
        --skip-tls-verify \
        -vvvv \
        {{ARGS}}

# Show help for the proposer binary
help:
    cargo run --bin base-proposer -- --help

# Build Docker image
docker-build:
    docker build -t proposer:local -f docker/Dockerfile .

# Run all checks (CI)
ci: fmt-check clippy test deny

# Generate documentation
doc:
    cargo doc --workspace --no-deps --open

# Update dependencies
update:
    cargo update

# Check for outdated dependencies
outdated:
    cargo outdated -R
